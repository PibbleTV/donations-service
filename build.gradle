

plugins {
    id 'java'
    id 'org.springframework.boot' version '3.4.4'
    id 'io.spring.dependency-management' version '1.1.7'
    id "org.sonarqube" version "6.2.0.5505"
    id "io.sentry.jvm.gradle" version "5.7.0"
    id 'jacoco'
    id 'com.github.node-gradle.node' version '7.0.2'
}

def envFile = file('.env')
def envProps = new Properties()
if (envFile.exists()) {
    envFile.withInputStream { stream ->
        envProps.load(stream)
    }
}

sonar {
    properties {
        property "sonar.java.coveragePlugin", "jacoco"
        property "sonar.qualitygate.wait", true
        property "sonar.coverage.jacoco.xmlReportPaths", "build/reports/jacoco/test/jacocoTestReport.xml"
        property "sonar.coverage.exclusions", [
                'src/**/com/pibbletv/donations_service/controller/DonationController.java',
                'src/**/com/pibbletv/donations_service/business/converters/DonationConverter.java',
                'src/**/com/pibbletv/donations_service/DonationsServiceApplication.java',
        ]
        property "sonar.cpd.exclusions", [
                'src/**/com/pibbletv/donations_service/controller/DonationController.java',
                'src/**/com/pibbletv/donations_service/business/converters/DonationConverter.java',
                'src/**/com/pibbletv/donations_service/DonationsServiceApplication.java',
        ]
    }
}

sentry {
    includeSourceContext = true
    org = "pibbletv"
    projectName = "donations-service"
    authToken = System.getenv('SENTRY_AUTH_TOKEN')
    // New secret because I accidentally leaked the old one :(
}

group = 'com.pibbletv'
version = '0.0.1-SNAPSHOT'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'io.sentry:sentry-spring-boot-starter:8.12.0'
    implementation 'org.springframework.boot:spring-boot-starter-data-r2dbc'
    implementation 'io.asyncer:r2dbc-mysql:1.0.2'
    implementation 'jakarta.validation:jakarta.validation-api:3.0.2'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'io.micrometer:micrometer-registry-prometheus'
    implementation 'org.springframework.boot:spring-boot-starter-webflux'
    compileOnly 'org.projectlombok:lombok'
    runtimeOnly 'com.h2database:h2'
    runtimeOnly 'io.r2dbc:r2dbc-h2'
    annotationProcessor 'org.projectlombok:lombok'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'io.projectreactor:reactor-test'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

jacoco {
    toolVersion = "0.8.7"
}

jacocoTestReport {
    dependsOn test
    reports {
        xml.required = true
        html.required = true
    }
}

test {
    useJUnitPlatform()
    finalizedBy jacocoTestReport
}

tasks.named('test') {
    useJUnitPlatform()
}

tasks.withType(Test) {
    systemProperty 'eureka.client.enabled', 'false'
}
